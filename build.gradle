group 'com.thomsonreuters.lsps'
version '1.0-SNAPSHOT'

apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.7

repositories {
    mavenCentral()
}

dependencies {
    compile 'commons-io:commons-io:2.4'
    compile 'org.codehaus.groovy:groovy-all:2.3.11'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'TR LSPS IO Utils Library'
                packaging 'jar'
                description 'TR LSPS IO Utils Library'
                url 'https://github.com/ThomsonReuters-LSPS/tr-lsps-io'

                issueManagement {
                    url 'https://github.com/ThomsonReuters-LSPS/tr-lsps-io/issues'
                    system 'GitHub Issues'
                }

                scm {
                    connection 'scm:git:git://github.com/ThomsonReuters-LSPS/tr-lsps-io.git'
                    developerConnection 'scm:git:git@github.com/ThomsonReuters-LSPS/tr-lsps-io.git'
                    url 'https://github.com/ThomsonReuters-LSPS/tr-lsps-io'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'http://www.opensource.org/licenses/mit-license.php'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'mirasrael'
                        name 'Alexander Bondarev'
                        email 'alexander.bondarev@thomsonreuters.com'
                        url 'https://github.com/mirasrael'
                    }
                }
            }
        }
    }
}
